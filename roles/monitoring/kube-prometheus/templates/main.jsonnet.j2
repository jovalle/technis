# Source: https://raw.githubusercontent.com/prometheus-operator/kube-prometheus/main/example.jsonnet

local ingress(metadata, domain, service) = {
  apiVersion: 'networking.k8s.io/v1',
  kind: 'Ingress',
  metadata: metadata,
  spec: {
    rules: [{
      host: domain,
      http: {
        paths: [{
          path: '/',
          pathType: 'Prefix',
          backend: {
            service: service,
          },
        }],
      },
    }],
  },
};

local kp =
  (import 'kube-prometheus/main.libsonnet') +
  (import 'kube-prometheus/addons/networkpolicies-disabled.libsonnet') +
  {
    values+:: {
      common+: {
        namespace: 'monitoring',
        baseDomain: 'techn.is',
      },
      grafana+: {
        datasources: [
          {
            "access": "proxy",
            "editable": false,
            "name": "prometheus",
            "orgId": 1,
            "type": "prometheus",
            "url": "http://prometheus-k8s.monitoring.svc:9090",
            "version": 1
          },
          {
            "access": "proxy",
            "database": "monitoring",
            "editable": true,
            "name": "influxdb",
            "orgId": 1,
            "password": "{{ influxdb_pass | default(omit) }}",
            "user": "admin",
            "type": "influxdb",
            "url": "http://influxdb.monitoring.svc:8086",
            "version": 1
          },
          {
            "access": "proxy",
            "editable": true,
            "name": "loki",
            "org_id": 1,
            "type": "loki",
            "url": "http://loki.monitoring.svc:3100",
            "version": 1,
          },
          {
            "access": "proxy",
            "editable": true,
            "name": "prometheus-ext-truenas",
            "orgId": 1,
            "type": "prometheus",
            "url": "http://192.168.1.11:10086",
            "version": 1
          },
        ],
        plugins: [
          'flant-statusmap-panel',
          'grafana-clock-panel',
          'grafana-piechart-panel',
          'grafana-simple-json-datasource',
        ],
        dashboards+:: {
          'idrac-host-stats.json': (import 'dashboards/idrac-host-stats.json'),
          'pi-hole-exporter.json': (import 'dashboards/pi-hole-exporter.json'),
        },
      },
      other: {},
    },
    alertmanager+: {
      alertmanager+: {
        spec+: {
          externalUrl: 'https://alertmanager.' + $.values.common.baseDomain,
        },
      },
      ingress: ingress(
        { name: 'grafana' },
        'grafana.' + $.values.common.baseDomain,
        {
          name: $.grafana.service.metadata.name,
          port: {
            name: $.grafana.service.spec.ports[0].name,
          },
        },
      ),
    },
    grafana+: {
      ingress: ingress(
        {
          name: 'grafana',
          namespace: $.values.common.namespace,
        },
        'grafana.' + $.values.common.baseDomain,
        {
          name: $.grafana.service.metadata.name,
          port: {
            name: $.grafana.service.spec.ports[0].name,
          },
        },
      ),
    },
    prometheus+: {
      prometheus+: {
        spec+: {
          externalUrl: 'https://prometheus.' + $.values.common.baseDomain,
          replicas: 1,
          retention: '7d',
          retentionSize: '40GB',
          storage: {
            volumeClaimTemplate: {
              metadata: {
                name: 'promdata',
              },
              spec: {
                storageClassName: 'longhorn',
                accessModes: ['ReadWriteOnce'],
                resources: {
                  requests: { storage: '40Gi' },
                },
              },
            },
          },
          additionalScrapeConfigs: {
            name: 'additional-scrape-configs',
            key: 'prometheus-additional.yaml'
          },
        },
      },
      ingress: ingress(
        {
          name: 'prometheus',
          namespace: $.values.common.namespace,
        },
        'prometheus.' + $.values.common.baseDomain,
        {
          name: $.prometheus.service.metadata.name,
          port: {
            name: $.prometheus.service.spec.ports[0].name,
          },
        },
      ),
    },
  };

{ 'setup/0namespace-namespace': kp.kubePrometheus.namespace } +
{
  ['setup/prometheus-operator-' + name]: kp.prometheusOperator[name]
  for name in std.filter((function(name) name != 'serviceMonitor' && name != 'prometheusRule'), std.objectFields(kp.prometheusOperator))
} +
// serviceMonitor and prometheusRule are separated so that they can be created after the CRDs are ready
{ 'prometheus-operator-serviceMonitor': kp.prometheusOperator.serviceMonitor } +
{ 'prometheus-operator-prometheusRule': kp.prometheusOperator.prometheusRule } +
{ 'kube-prometheus-prometheusRule': kp.kubePrometheus.prometheusRule } +
{ ['alertmanager-' + name]: kp.alertmanager[name] for name in std.objectFields(kp.alertmanager) } +
{ ['blackbox-exporter-' + name]: kp.blackboxExporter[name] for name in std.objectFields(kp.blackboxExporter) } +
{ ['grafana-' + name]: kp.grafana[name] for name in std.objectFields(kp.grafana) } +
{ ['kube-state-metrics-' + name]: kp.kubeStateMetrics[name] for name in std.objectFields(kp.kubeStateMetrics) } +
{ ['kubernetes-' + name]: kp.kubernetesControlPlane[name] for name in std.objectFields(kp.kubernetesControlPlane) }
{ ['node-exporter-' + name]: kp.nodeExporter[name] for name in std.objectFields(kp.nodeExporter) } +
{ ['prometheus-' + name]: kp.prometheus[name] for name in std.objectFields(kp.prometheus) } +
{ ['prometheus-adapter-' + name]: kp.prometheusAdapter[name] for name in std.objectFields(kp.prometheusAdapter) }
